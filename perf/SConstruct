
import os

Import('env')

rrperf_env = env.Clone()
perf_env = env.Clone()

rrperf_env["CC"] = env["TESTCC"]
rrperf_env["CXX"] = env["TESTCXX"]
rrperf_env["LINK"] = env["TESTLINK"]

rrperfs = [ ]
perfs = [ ]
for f in Glob('#perf/*.c'):
    fn = os.path.basename(f.rstr()[0:-2])
    if fn.startswith("rr_"):
        rrperfs.append(fn)
    else:
        perfs.append(fn)

perfbench_deps = [ "CastorRuntime",
                   "CastorThreadRuntime",
                   "CastorPass",
                   Glob("#perf/*.c") ]

rrperf_env.Append(CPPFLAGS = ["--sysroot=$SYSROOT",
                              "-Xclang", "-load", "-Xclang", "$CASTORPASS"])
rrperf_env.Append(LINKFLAGS = ["--sysroot=$SYSROOT"])
rrperf_env.Append(LIBS = ["thr", "c"])
perf_env.Append(LIBS = ["thr", "c"])

for t in perfs:
    t = perf_env.Program(t, [ t+".c" ])
    perfbench_deps.append(t)

for t in rrperfs:
    rrperf_env.Depends(t+".o",
                       ["CastorRuntime", "CastorThreadRuntime", "CastorPass"])
    t = rrperf_env.Program([t+".c"])
    perfbench_deps.append(t)

AlwaysBuild(Alias('perfbench', perfbench_deps, "perf/perfbench.py"))


