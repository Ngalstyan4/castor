
import os

Import('env')

test_env = env.Clone()

test_env["CC"] = test_env["TESTCC"]
test_env["CXX"] = test_env["TESTCXX"]
test_env["LINK"] = test_env["TESTLINK"]

tests = [ ]
for f in Glob('#unit-tests/*.c'):
    tests.append(os.path.basename(f.rstr()[0:-2]))

testbench_deps = [ "CastorRuntime",
                   "CastorThreadRuntime",
                   "CastorPass",
                   Glob("#unit-tests/*.c") ]

test_env.Append(CPPFLAGS = ["--sysroot=$SYSROOT",
                            "-Xclang", "-load", "-Xclang", "$CASTORPASS"])
test_env.Append(LINKFLAGS = ["--sysroot=$SYSROOT",
                             "-Wl,-rpath=${SYSROOT}usr/lib"])
test_env.Append(LIBS = ["thr", "c"])

for t in tests:
    test_env.Depends(t+".o",
                     ["CastorRuntime", "CastorThreadRuntime", "CastorPass"])
    t = test_env.Program(t, [t+".c"])
    testbench_deps.append(t)

AlwaysBuild(Alias('testbench', testbench_deps, "unit-tests/testbench.py"))

